buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath('com.anatawa12.forge:ForgeGradle:2.3-1.0.+') {
			changing = true
		}
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'

def ENV = System.getenv()
version = ENV.BUILD_NUMBER ? "${mod_version}.${ENV.BUILD_NUMBER}" : "${mod_version}.2"
group = package_group
archivesBaseName = mod_name

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
	sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
	version = "1.12.2-14.23.5.2838"
	runDir = "run"
	mappings = "${mappings_version}"

	replace "0.0.0.ftblib", project.version
	replaceIn "${mod_reference}"
}

dependencies {
	compileOnly fileTree(dir: project.lib, include: ['*.jar'], exclude: ['*-sources.jar'])
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

jar {
	manifest {
		attributes 'FMLAT': 'ftblib_at.cfg'
	}
}

artifacts {
	archives sourceJar
}

tasks.register('deobfJar', Jar) {
	from sourceSets.main.output
	archiveClassifier.set('deobf')
}

build.finalizedBy deobfJar
